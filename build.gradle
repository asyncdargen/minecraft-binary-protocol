plugins {
    id 'java'
    id 'maven-publish'
}

group 'ru.dargen'
version '1.0.0'
archivesBaseName = "mc-binary-protocol"

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://repo.starfarm.fun/releases' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.19.4-R0.1-SNAPSHOT'

    implementation 'ru.dargen:crowbar-core:1.0.4'
    implementation('ru.dargen:crowbar-asm:1.0.3') {
        exclude group: 'org.ow2.asm'
    }

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url 'https://repo.starfarm.fun/releases'
            credentials {
                username System.getenv('SF_REPO_USER')
                password System.getenv('SF_REPO_PASSWORD')
            }
        }

    }

    publications {
        maven(MavenPublication) {
            groupId group
            artifactId project.name
            version version
            from project.components.java
        }
    }
}

jar {
    from configurations.runtimeClasspath.collect(project.&zipTree)
}

tasks.withType(JavaCompile)*.options*.encoding = 'UTF-8'